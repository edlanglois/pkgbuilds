# Maintainer: Eric Langlois <eric@langlois.xyz>
# Contributor: Zhuoyun Wei <wzyboy@wzyboy.org>
# Contributor: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)

pkgname=python-magic-ahupp
_name='python-magic'
pkgdesc='A python wrapper for libmagic'
pkgver=0.4.24
pkgrel=1
arch=('any')
url="https://pypi.org/project/python-magic"
license=('MIT')
depends=('file')
makedepends=('python-setuptools')
conflicts=('python-magic')
provides=('python-magic')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('de800df9fb50f8ec5974761054a708af6e4246b03b4bdaee993f948947b0ebcf')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver"
	export LC_ALL=en_US.UTF-8
	# For some reason the test/testdata directory is missing
	# Just run a couple basic tests that don't use it.
	python -m pytest "test/test.py" -k "test_version or test_getparam" --verbose
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D -m644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
