# Maintainer: Eric Langlois <eric@langlois.xyz>
pkgname=python-spinup-edlanglois-git
_pyname=spinup
pkgver=0.2.r15.c6b4b7a
pkgrel=1
pkgdesc="An educational resource to help anyone learn deep reinforcement learning."
arch=("any")
url="https://spinningup.openai.com"
license=("MIT")
depends=(
	"python"
	"python-cloudpickle"
	"python-gym"
	"python-joblib"
	"python-matplotlib"
	"python-mpi4py"
	"python-numpy"
	"python-pandas"
	"python-psutil"
	"python-pytorch"
	"python-scipy"
	"python-seaborn"
	"python-tensorflow"
	"python-tqdm"
)
makedepends=("git" "python-setuptools")
checkdepends=("python-pytest")
provides=("${pkgname%-edlanglois-git}")
conflicts=("${pkgname%-edlanglois-git}")
source=(
	"${pkgname%-git}::git+https://github.com/edlanglois/spinningup.git#branch=master"
)
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --tags --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py build
}

check() {
	cd "$srcdir/${pkgname%-git}/build/lib"
	pytest "$srcdir/${pkgname%-git}/test"
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname%-git}/LICENSE"
}
