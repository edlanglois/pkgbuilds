# Maintainer: Eric Langlois <eric@langlois.xyz>
pkgname=python-humanfriendly
_name=${pkgname#python-}
pkgver=9.2
pkgrel=1
pkgdesc="Human friendly input/output in Python"
arch=('any')
url="https://humanfriendly.readthedocs.io"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('f7dba53ac7935fd0b4a2fc9a29e316ddd9ea135fb3052d3d0279d10c18ff9c48')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
