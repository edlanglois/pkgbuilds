# Maintainer: Eric Langlois <eric@langlois.xyz>
pkgname=python-simple-term-menu
_name=${pkgname#python-}
pkgver=1.3.0
pkgrel=1
pkgdesc="A Python package for simple interactive menus on the command line"
arch=('any')
url="https://github.com/IngoMeyer441/simple-term-menu"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('6acb4dd4eb00da0ba1d00cb263708fed4492803c3de8554c8336da013032e711')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
